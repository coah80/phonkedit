plugins {
    id 'net.saliman.properties' version '1.6.0'
    id 'fabric-loom' version '1.12-SNAPSHOT'
    id 'maven-publish'
}

version = '2.1.2'
group = 'com.phonkedit'
archivesBaseName = 'phonkedit'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabricmc.net/' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://maven.terraformersmc.com/releases/' }
    maven { url 'https://maven.ladysnake.org/releases' }
}

dependencies {
    minecraft "com.mojang:minecraft:${stonecutter.current.version}"
    mappings "net.fabricmc:yarn:${property('yarn_mappings')}:v2"
    modImplementation "net.fabricmc:fabric-loader:0.17.3"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${property('fabric_version')}"
    // Mod Menu API (version varies by stonecutter target)
    modImplementation "com.terraformersmc:modmenu:${property('modmenu_version')}"

    // Cloth Config API for in-game config screen
    modApi("me.shedaniel.cloth:cloth-config-fabric:${property('cloth_config_version')}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft", stonecutter.current.version

    filesMatching("fabric.mod.json") {
        expand(["version": project.version, "minecraft": stonecutter.current.version])
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
}

loom {
    runs {
        client2 {
            client()
            name "Minecraft Client 2"
            runDir "run2"
        }
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }
}
